buildscript {
	ext {
		kotlinVersion = '1.4.0'
		springBootVersion = '2.3.4.RELEASE'
	}
	ext['junit-jupiter.version']  = '5.5.2'

	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
		classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
		classpath('org.owasp:dependency-check-gradle:5.2.4')
	}
}

ext {
	springCloudVersion = 'Hoxton.SR8'
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.owasp.dependencycheck'

group = 'com.boclips.api'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
compileKotlin {
	kotlinOptions {
		freeCompilerArgs = ["-Xjsr305=strict"]
		jvmTarget = "1.8"
	}
}
compileTestKotlin {
	kotlinOptions {
		freeCompilerArgs = ["-Xjsr305=strict"]
		jvmTarget = "1.8"
	}
}

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/milestone" }
	maven { url 'https://jitpack.io' }
}

test {
	useJUnitPlatform()
}

dependencies {
	implementation('org.springframework.cloud:spring-cloud-starter-gateway')
	implementation('org.springframework.boot:spring-boot-starter-actuator')

	implementation('org.jetbrains.kotlin:kotlin-stdlib-jdk8')
	implementation('org.jetbrains.kotlin:kotlin-reflect')
	implementation('org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.1')
	implementation('io.github.microutils:kotlin-logging:1.8.3')

	implementation('com.fasterxml.jackson.module:jackson-module-kotlin')
	implementation('com.fasterxml.jackson.core:jackson-databind:2.10.1')
	implementation('com.fasterxml.jackson.core:jackson-core:2.10.1')
	implementation('com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.10.1')

	implementation('com.auth0:java-jwt:3.8.2')
	implementation 'org.springframework.hateoas:spring-hateoas'

	implementation('io.micrometer:micrometer-core')
	implementation('io.micrometer:micrometer-registry-prometheus')

	implementation('io.sentry:sentry-logback:1.7.21')
	implementation('io.opentracing.contrib:opentracing-spring-jaeger-cloud-starter:3.2.2')
	implementation("com.github.boclips:user-service:283")
	// Required by user-service client
	implementation 'io.github.openfeign:feign-core:10.7.0'

	testImplementation('org.springframework.boot:spring-boot-starter-test')
	testImplementation('org.springframework.cloud:spring-cloud-contract-wiremock')
	testImplementation('org.junit.jupiter:junit-jupiter-api')
	testImplementation('org.junit.jupiter:junit-jupiter-params')
	testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine')
	testImplementation('com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0')
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

dependencyCheck {
	failBuildOnCVSS = 7 // Fail on high severity or greater

	data {
		directory = './dependency-check-cache'
	}

	analyzers {
		assemblyEnabled = false
	}

	suppressionFile = './dependency-check-suppressions.xml'

	outputDirectory = 'security-report'
}

//https://github.com/spring-cloud/spring-cloud-gateway/issues/319
configurations {
	runtime.exclude module: 'spring-boot-starter-web'
}